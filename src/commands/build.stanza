defpackage poet/commands/build:
  import core
  import core/parsed-path
  import collections

  import maybe-utils
  import semver
  import term-colors
  import toml/file
  import toml/parser
  import toml/table
  import toml/value

  import poet/dependency
  import poet/dependencies
  import poet/file-utils
  import poet/lock
  import poet/logging
  import poet/process-utils

public defn ensure-poet-dir-structure-exists () -> False:
  defn create-dir! (path: String) -> True|False:
    if not file-exists?(path):
      create-dir(path)
      true

  if create-dir!(".poet/"):
    create-dir!(".poet/deps/")
    create-dir!(".poet/pkgs/")
  false

defn write-build-stanza-proj (dependencies: Tuple<Dependency>) -> False:
  within f = open(".poet/stanza.proj", false):
    val deps-dir = to-string("%_/.poet/deps/" % [get-cwd()])
    for dep in dependencies do:
      val dep-path = get-cwd() $> parse-path $> relative-to-dir{_, dep $> /path}
      println(f, to-string(\<>include "%_/stanza.proj"<> % [dep-path]))
    println(f, \<>include "../stanza.proj"<>)

defn write-poet-lock-file (dependencies: Tuple<Dependency>) -> False:
  within f = open("poet.lock", false):
    for dep in dependencies do:
      match(dep):
        (dep: GitDependency): 
          println(f, "%_={locator=%~,version=%~,hash=%~}"
                  % [name(dep), locator(dep), to-string(version(dep)), hash(dep)])
        (dep: PathDependency): 
          println(f, "%_={}" % [name(dep)])

public defn build (args0: Tuple<String>) -> False:
  debug("with args '%s'" % [args0])

  ensure-poet-dir-structure-exists()

  val dependencies = poet/dependencies/fetch-and-sync()

  write-build-stanza-proj(dependencies)
  write-poet-lock-file(dependencies)

  val poet-dir = to-string("%_/.poet" % [get-cwd()])

  val args = to-tuple $ cat-all([["stanza", "build"], args0, ["-pkg", "pkgs"]])
  ProcessBuilder(args)
    $> in-dir{_, poet-dir}
    $> build
    $> wait-process-throw-on-nonzero{_, "build failed!"}
    
  false
