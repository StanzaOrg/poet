defpackage poet/commands/add:
  import core
  import poet/utils
  import poet/dependency
  import poet/config

public defn add (kind: String, dependency: String) -> False:
  defn error-incorrectly-formed () -> Void:
    error("incorrectly formatted dependency: `%_`" % [dependency])

  defn parse-git-dependency (full-locator: String) -> Dependency:
    match(split-last(full-locator, '/')):
      ([leading-url, name]: [String, String]):
        Dependency(GitLocator(full-locator), strip-end(name, ".git"))
      (_):
        error-incorrectly-formed()

  defn parse-path-dependency (path: String) -> Dependency:
    if not file-exists?(to-string("%_/poet.toml" % [path])):
      error("no `poet.toml` at path dependency `%_`" % [path])
    else:
      match(base-name?(path)):
        (dir: One<String>):
          Dependency(PathLocator(path), value!(dir))
        (_: None):
          error-incorrectly-formed()

  val current-dir = resolve-path!(".")
  val poet-toml-path = to-string("%_/poet.toml" % [current-dir])
  if not file-exists?(poet-toml-path):
    error("poet.toml doesn't exist, was the project initialized?")

  val dep = switch(kind):
    "--git": parse-git-dependency(dependency)
    "--path": parse-path-dependency(dependency)
    else: error("add: unknown argument `%_`" % [kind])

  within f = open(poet-toml-path, true):
    println(f, "%_ = \"%_\"" % [name(dep), location(dep)])
